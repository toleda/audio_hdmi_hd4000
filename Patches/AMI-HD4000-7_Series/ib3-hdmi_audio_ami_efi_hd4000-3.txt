# Maintained by: toleda for: github.com/toleda/audio_hdmi_hd4000
# File: ib3-hdmi_audio_ami_efi_hd4000-3.txt_v3.0
# Credit: PikeRAlpha, PJALM, RehabMan, SL_UnderWater
#
# Desktop HD4K/AMD/Nvidia HDMI audio dsdt edits (Audio ID: 3)
# Enables HD4K/AMD/Nvidia AirPlay Mirroring.
# IB/Framebuffer: 0166 001A Macmini6,1 - FB/HDMI connector/port 0x7
#
# NOTE: Run this patch file only once on a dsdt.  
# If you must run this patch again: delete Steps 1, 2 and 3, 
# patch with Steps 4, 5 and 6 only.
#
# 1. Remove PEGP device - if present
# 2. Rename GFX0 devices to IGPU - Verify IOReg/GFXO@2
# 3. Rename device PEG0 to device PEGP - Verify IOReg/PEG0@1
# 4. Insert device GFX0 and device HDAU into device PEGP (Nvidia/AMD HDMI audio)
# 5. Insert HDMI audio injection into device IGPU (HD4K HDMI audio - Part 1/2)
# 6. Insert HDMI audio injection into device HDEF (HD4K HDMI audio - Part 2/2)
#
# 1. Remove PEGP device
into device label PEGP remove_entry;
into scope label _SB.PCI0.PEG0.PEGP remove_entry;
into_all all code_regex Notify\s\(\\_SB.PCI0.PEG0.PEGP,\s0x.*\) removeall_matched;
# 2. Rename GFX0 devices to IGPU
into device label GFX0 set_label begin IGPU end;
into_all all code_regex GFX0 replaceall_matched begin IGPU end;
into scope label _SB.PCI0.GFX0 set_label begin _SB.PCI0.IGPU end;
into scope label \_SB.PCI0.GFX0 set_label begin \_SB.PCI0.IGPU end;
into scope label _SB.PCI0.PEG0.GFX0 set_label begin _SB.PCI0.PEGP.IGPU end;
# 3. Rename device PEG0 to device PEGP
into device label PEG0 set_label begin PEGP end;
into_all all code_regex PEG0 replaceall_matched begin PEGP end;
into scope label _SB.PCI0.PEG0 set_label begin _SB.PCI0.PEGP end;
into scope label \_SB.PCI0.PEG0 set_label begin \_SB.PCI0.PEGP end;
# 
# To patch more than once, delete from "Run this patch once â€¦" (above) to here.
#
# 4.  Insert device GFX0 and device HDAU (Nvidia/AMD HDMI audio)
into method label _DSM parent_adr 0x00010000 remove_entry;
into device name_adr 0x00010000 insert
begin
Device (GFX0)\n
{\n
    Name (_ADR, Zero)\n
    Name (_SUN, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
	    If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	    Return (Package()\n
            {\n
                "@0,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "@1,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "@2,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "@3,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "@4,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "@5,connector-type", Buffer() { 0x00, 0x08, 0x00, 0x00 },\n
                "hda-gfx", Buffer() { "onboard-2" },\n
	    })\n
    }\n
}\n
Device (HDAU)\n
{\n
    Name (_ADR, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
	    If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	    Return (Package()\n
            {\n
               "hda-gfx", Buffer() { "onboard-2" },\n
	    })\n
    }\n
}\n
end;
# 5. Insert HDMI audio injection into device IGPU (HD4K HDMI audio - Part 1/2)
into method label _DSM parent_adr 0x00020000 remove_entry;
into device name_adr 0x00020000 insert
begin
    Method (_DSM, 4, NotSerialized)\n
    {\n
	    If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	    Return (Package()\n
	    {\n
	        "device-id", Buffer() { 0x66, 0x01, 0x00, 0x00 },\n
	        "AAPL,ig-platform-id", Buffer() { 0x0a, 0x00, 0x66, 0x01 },\n
	        "hda-gfx", Buffer() { "onboard-1" },\n
	    })\n
    }\n
end;
# 6. Insert HDMI audio injection into device HDEF (HD4K HDMI audio - Part 2/2)
into method label _DSM parent_adr 0x001B0000 remove_entry;
into device name_adr 0x001B0000 insert
begin
    Method (_DSM, 4, NotSerialized)\n
    {\n
	    If (LEqual (Arg2, Zero)) { Return (Buffer() { 0x03 } ) }\n
	    Return (Package()\n
	    {\n
                "layout-id", Buffer() { 0x03, 0x00, 0x00, 0x00 },\n
                "PinConfigurations", Buffer(Zero) {},\n
  	        "hda-gfx", Buffer() { "onboard-1" },\n
	    })\n
    }\n
end;

into definitionblock code_regex . insert
begin Store ("ib3-hdmi_audio_ami_efi_hd4000-3.txt_v3.0 dsdt edits, github.com/toleda", Debug) end
