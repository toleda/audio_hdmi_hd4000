# Maintained by: toleda for: github.com/toleda/audio_hdmi_hd4000
# File: ib2-hdmi_audio_ami_efi_hd4000-1.txt_v1.0
# Adds Desktop AMD/Nvidia/HD4000 HDMI audio dsdt edits  (Audio ID: 1)
#
# 1. Remove PEGP device - if present
# 2. Rename GFX0 devices to IGPU - Verify IOReg/GFXO@2
# 3. Rename device PEG0 to device PEGP - Verify IOReg/PEG0@1
# 4. Add method DTGP to the main block - if not present
# 5. Insert device GFX0 and device HDAU into device PEGP (Nvidia/AMD HDMI audio)
# 6. Insert HDMI audio injection into device IGPU (HD4000 HDMI audio - Part 1/2)
# 7. Insert HDMI audio injection into device HDEF (HD4000 HDMI audio - Part 2/2)
#
# 1. Remove PEGP device
into device label PEGP remove_entry;
into scope label _SB.PCI0.PEG0.PEGP remove_entry;
into_all all code_regex Notify\s\(\\_SB.PCI0.PEG0.PEGP,\s0x.*\) removeall_matched;
# 2. Rename GFX0 devices to IGPU
into device label GFX0 set_label begin IGPU end;
into_all all code_regex GFX0 replaceall_matched begin IGPU end;
into scope label _SB.PCI0.GFX0 set_label begin _SB.PCI0.IGPU end;
into scope label \_SB.PCI0.GFX0 set_label begin \_SB.PCI0.IGPU end;
into scope label _SB.PCI0.PEG0.GFX0 set_label begin _SB.PCI0.PEGP.IGPU end;
# 3. Rename device PEG0 to device PEGP
into device label PEG0 set_label begin PEGP end;
into_all all code_regex PEG0 replaceall_matched begin PEGP end;
into scope label _SB.PCI0.PEG0 set_label begin _SB.PCI0.PEGP end;
into scope label \_SB.PCI0.PEG0 set_label begin \_SB.PCI0.PEGP end;
# 4. Add method DTGP to the main block, it's used by other patches
into method label DTGP remove_entry;
into definitionblock code_regex . insert
begin
Method (DTGP, 5, NotSerialized)\n
{\n
    If (LEqual (Arg0, Buffer (0x10)\n
            {\n
                /* 0000 */    0xC6, 0xB7, 0xB5, 0xA0, 0x18, 0x13, 0x1C, 0x44, \n
                /* 0008 */    0xB0, 0xC9, 0xFE, 0x69, 0x5E, 0xAF, 0x94, 0x9B\n
            }))\n
    {\n
        If (LEqual (Arg1, One))\n
        {\n
            If (LEqual (Arg2, Zero))\n
            {\n
                Store (Buffer (One)\n
                    {\n
                        0x03\n
                    }, Arg4)\n
                Return (One)\n
            }\n
            If (LEqual (Arg2, One))\n
            {\n
                Return (One)\n
            }\n
        }\n
    }\n
    Store (Buffer (One)\n
        {\n
            0x00\n
        }, Arg4)\n
    Return (Zero)\n
}
end;
# 5.  Insert device GFX0 and device HDAU (Nvidia/AMD HDMI audio)
into method label _DSM parent_adr 0x00010000 remove_entry;
into device name_adr 0x00010000 insert
begin
Device (GFX0)\n
{\n
    Name (_ADR, Zero)\n
    Name (_SUN, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x0a)\n
            {\n
                "@0,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@1,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@2,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@3,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-2" \n
                }, \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
  }\n
Device (HDAU)\n
{\n
    Name (_ADR, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x02)\n
            {\n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-2" \n
                }, \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
  }\n
end;
# 6. Insert HDMI audio injection into device IGPU (HD4000 HDMI audio - Part 1/2)
into method label _DSM parent_adr 0x00020000 remove_entry;
into device name_adr 0x00020000 insert
begin
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x06)\n
 	     {\n
                "device-id", \n
                Buffer (0x04) \n
                { \n
                   0x66, 0x01, 0x00, 0x00 \n
                }, \n
                "AAPL,ig-platform-id", \n
                Buffer (0x04) \n
                { \n
                   0x0A, 0x00, 0x66, 0x01 \n
                }, \n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-1" \n
                } \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
end;
# 7. Insert HDMI audio injection into device HDEF (HD3=4000 HDMI audio - Part 2/2)
into method label _DSM parent_adr 0x001B0000 remove_entry;
into device name_adr 0x001B0000 insert
begin
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x06)\n
 	     {\n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-1" \n
                }, \n
                "layout-id", \n
                Buffer (0x04)\n
                {\n
                    0x01, 0x00, 0x00, 0x00\n
                }, \n
                "PinConfigurations", \n
                Buffer (Zero) {}\n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
end;
into definitionblock code_regex . insert
begin Store ("ib2-hdmi_audio_ami_efi_hd4000-1.txt_v1.0 dsdt edits, github.com/toleda", Debug) end
